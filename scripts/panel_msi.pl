#!/usr/bin/env perl
### multimmr_msi.pl ################################################################################
use AutoLoader 'AUTOLOAD';
use strict;
use warnings;
use version;
use Carp;
use Getopt::Std;
use Getopt::Long;
use POSIX qw(strftime);
use File::Basename;
use File::Path qw(make_path);
use YAML qw(LoadFile);
use List::Util qw(any sum);
use IO::Handle;

my $cwd = dirname(__FILE__);
require "$cwd/utilities.pl";

our ($reference, $ref_type, $pon);

####################################################################################################
# version	author		comment
# 1.0		sprokopec	script to run simple MSI classifier for MultiMMR

### USAGE ##########################################################################################
# multimmr_msi.pl -t tool.yaml -d data.yaml -o /path/to/output/dir -c slurm --remove --dry_run
#
# where:
# 	-t (tool.yaml) contains tool versions and parameters, reference information, etc.
# 	-d (data.yaml) contains sample information (YAML file containing paths to BWA-aligned,
# 	GATK-processed BAMs, generated by gatk.pl)
# 	-o (/path/to/output/dir) indicates tool-specific output directory
# 	-c indicates hpc driver (ie, slurm)
# 	--remove indicates that intermediates will be removed
# 	--dry_run indicates that this is a dry run

### DEFINE SUBROUTINES #############################################################################
# format command to run MSI classifier
sub get_msi_command {
	my %args = (
		step		=> undef,
		intervals	=> undef,
		output_file	=> undef,
		bam		=> undef,
		tumour_rc	=> undef,
		normal_rc	=> undef,
		create_pon	=> 'N',
		pon_dir		=> undef,
		pon		=> undef,
		@_
		);

	my $msi_command = join(' ',
		"Rscript $cwd/runPanelMSI.R",
		"--step", $args{step},
		"--targets", $args{intervals},
		"--output", $args{output_file}
		);

	if (defined($args{bam})) {
		$msi_command .= " --bam $args{bam}";
		}

	if (defined($args{tumour_rc})) {
		$msi_command .= " --tumour_freq $args{tumour_rc}";
		}

	if (defined($args{normal_rc})) {
		$msi_command .= " --normal_freq $args{normal_rc}";
		}

	if ( ('Y' eq $args{create_pon}) & (defined($args{pon_dir})) ) {
		$msi_command .= " --pon_dir $args{pon_dir}";
		}

	if (defined($args{pon})) {
		$msi_command .= " --pon $args{pon}";
		}

	return($msi_command);
	}

### MAIN ###########################################################################################
sub main {
	my %args = (
		tool_config		=> undef,
		data_config		=> undef,
		output_directory	=> undef,
		hpc_driver		=> undef,
		del_intermediates	=> undef,
		dry_run			=> undef,
		no_wait			=> undef,
		@_
		);

	### PREAMBLE ######################################################################################
	unless($args{dry_run}) {
		print "Initiating panelMSI pipeline...\n";
		}

	# load tool config
	my $tool_data_orig = LoadFile($args{tool_config});
	my $tool_data = error_checking(tool_data => $tool_data_orig, pipeline => 'panelmsi');
	my $date = strftime "%F", localtime;

	# organize output and log directories
	my $output_directory = $args{output_directory};
	$output_directory =~ s/\/$//;

	my $log_directory = join('/', $output_directory, 'logs');
	unless(-e $log_directory) { make_path($log_directory); }

	my $log_file = join('/', $log_directory, 'run_panelMSI_pipeline.log');

	# create a file to hold job metrics
	my (@files, $run_count, $outfile, $touch_exit_status);
	unless ($args{dry_run}) {
		# initiate a file to hold job metrics
		opendir(LOGFILES, $log_directory) or die "Cannot open $log_directory";
		@files = grep { /slurm_job_metrics/ } readdir(LOGFILES);
		$run_count = scalar(@files) + 1;
		closedir(LOGFILES);

		$outfile = $log_directory . '/slurm_job_metrics_' . $run_count . '.out';
		$touch_exit_status = system("touch $outfile");
		if (0 != $touch_exit_status) { Carp::croak("Cannot touch file $outfile"); }

		$log_file = join('/', $log_directory, 'run_panelMSI_pipeline_' . $run_count . '.log');
		}

	# start logging
	open (my $log, '>', $log_file) or die "Could not open $log_file for writing.";
	$log->autoflush;

	print $log "---\n";
	print $log "Running panelMSI pipeline.\n";
	print $log "\n  Tool config used: $args{tool_config}";
	print $log "\n    Reference used: $tool_data->{reference}";

	$ref_type  = $tool_data->{ref_type};

	print $log "\n    Target intervals: $tool_data->{targets_bed}";
	print $log "\n    Output directory: $output_directory";
	print $log "\n  Sample config used: $args{data_config}";
	print $log "\n---\n";

	# set tools and versions
	my $r_version	= 'R/'. $tool_data->{r_version};

	# get user-specified tool parameters
	my $parameters = $tool_data->{panel_msi}->{parameters};

	# get optional HPC group
	my $hpc_group = defined($tool_data->{hpc_group}) ? "-A $tool_data->{hpc_group}" : undef;

	### RUN ###########################################################################################
	my ($run_script, $run_id, $link, $step1_run_id, $pon_run_id);
	my ($should_run_final, $should_run_pon);
	my (@all_jobs, @pon_jobs);
	my (%final_outputs, %normal_jobs, %patient_jobs, %cleanup);

	# find/annotate MSI sites within target regions
	my $annotated_targets = join('/', $output_directory, 'target_microsatellite_regions.bed');

	my $annotate_targets_command = get_msi_command(
		step	=> 'find_sites',
		intervals	=> $tool_data->{targets_bed},
		output_file	=> $annotated_targets
		);

	# check if this should be run
	if ('Y' eq missing_file($annotated_targets)) {

		# record command (in log directory) and then run job
		print $log "  >> Submitting job to find MSI sites...\n";

		$run_script = write_script(
			log_dir	=> $log_directory,
			name	=> 'find_msi_targets',
			cmd	=> $annotate_targets_command,
			modules	=> [$r_version],
			max_time	=> $parameters->{scan}->{time},
			mem		=> $parameters->{scan}->{mem},
			hpc_driver	=> $args{hpc_driver},
			extra_args	=> [$hpc_group]
			);

		$step1_run_id = submit_job(
			jobname		=> 'find_msi_targets',
			shell_command	=> $run_script,
			hpc_driver	=> $args{hpc_driver},
			dry_run		=> $args{dry_run},
			log_file	=> $log
			);

		push @all_jobs, $step1_run_id;
		} else {
		print $log "  >> Skipping 'Find panelMSI Sites' because this has already been completed!\n";
		}

	# get sample data
	my $smp_data = LoadFile($args{data_config});

	unless($args{dry_run}) {
		print "Processing " . scalar(keys %{$smp_data}) . " patients.\n";
		}

	my @normal_samples;
	foreach my $patient (sort keys %{$smp_data}) {
		my @normal_ids = keys %{$smp_data->{$patient}->{'normal'}};
		push @normal_samples, @normal_ids;
		}

	my @tumour_samples;
	foreach my $patient (sort keys %{$smp_data}) {
		my @tumour_ids = keys %{$smp_data->{$patient}->{'tumour'}};
		push @tumour_samples, @tumour_ids;
		}

	if (scalar(@tumour_samples) > 0) { $should_run_final = 1; }

	# are there any normals available to make a PoN?
	my $pon_directory = join('/', $output_directory, 'PanelOfNormals');
	if (scalar(@normal_samples) > 0) {
		unless(-e $pon_directory) { make_path($pon_directory) };
		} 

	if ( (!defined($tool_data->{panel_msi}->{pon})) && (scalar(@normal_samples) > 1) ) {
		$should_run_pon = 1;
		$pon = join('/', $pon_directory, $date . "_" . $ref_type . "_panelOfNormals_median.tsv");
		} elsif (defined($tool_data->{panel_msi}->{pon})) {
		$pon = $tool_data->{panel_msi}->{pon};
		print $log "Panel of Normals: $tool_data->{panel_msi}->{pon}";
		} else {
		die("No normals available and no PoN provided; this tool requires a 'healthy' baseline.");
		}

	# process each sample in $smp_data
	foreach my $patient (sort keys %{$smp_data}) {

		print $log "\nInitiating process for PATIENT: $patient";

		# find bams
		my @normal_ids = keys %{$smp_data->{$patient}->{'normal'}};

		# create some directories
		my $patient_directory = join('/', $output_directory, $patient);
		unless(-e $patient_directory) { make_path($patient_directory); }

		my $link_directory = join('/', $patient_directory, 'bam_links');
		unless(-e $link_directory) { make_path($link_directory); }

		# create an array to hold final outputs and all patient job ids
		$run_id = '';
		@normal_jobs{$patient} = [];
		@patient_jobs{$patient} = [];
		@final_outputs{$patient} = [];

		# if a normal is provided, create a WIG
		next if (scalar(@normal_ids) == 0);
		my $normal = $normal_ids[0];
		print $log "\n  NORMAL: $normal\n";

		# find input bam
		my $bam = $smp_data->{$patient}->{normal}->{$normal};
		$link = join('/', $link_directory, basename($bam));
		symlink($bam, $link);

		# generate command to collect readcounts
		my $normal_rc = join('/', $pon_directory, $normal . '_readcounts.tsv');

		my $make_rc_command = get_msi_command(
			step	=> 'get_readcounts',
			intervals	=> $annotated_targets,
			bam		=> $smp_data->{$patient}->{normal}->{$normal},
			output_file	=> $normal_rc
			);

		# check if this should be run
		if ('Y' eq missing_file($normal_rc)) {

			# record command (in log directory) and then run job
			print $log "  >> Submitting job for readCounter...\n";

			$run_script = write_script(
				log_dir	=> $log_directory,
				name	=> 'run_readCounter_' . $normal,
				cmd	=> $make_rc_command,
				modules	=> [$r_version],
				dependencies	=> $step1_run_id,
				max_time	=> $parameters->{readcounter}->{time},
				mem		=> $parameters->{readcounter}->{mem},
				hpc_driver	=> $args{hpc_driver},
				extra_args	=> [$hpc_group]
				);

			$run_id = submit_job(
				jobname		=> 'run_readCounter_' . $normal,
				shell_command	=> $run_script,
				hpc_driver	=> $args{hpc_driver},
				dry_run		=> $args{dry_run},
				log_file	=> $log
				);

			push @{$normal_jobs{$patient}}, $run_id;
			push @pon_jobs, $run_id;
			push @all_jobs, $run_id;
			} else {
			print $log "  >> Skipping readCounter because this has already been completed!\n";
			}
		}

	# create a panel of normals
	if ($should_run_pon) {

		# combine readcounts across all normal files
		my $make_pon_command = get_msi_command(
			step	=> 'make_pon',
			intervals	=> $tool_data->{targets_bed},
			create_pon	=> 'Y',
			pon_dir		=> $pon_directory,
			output_file	=> $pon
			);
		
		# check if this should be run
		if ('Y' eq missing_file($pon)) {

			# record command (in log directory) and then run job
			print $log ">> Submitting job to create Panel of Normals...\n";

			$run_script = write_script(
				log_dir	=> $log_directory,
				name	=> 'run_panel_msi__create_pon',
				cmd	=> $make_pon_command,
				modules	=> [$r_version],
				dependencies	=> join(':', @pon_jobs),
				max_time	=> $parameters->{create_pon}->{time},
				mem		=> $parameters->{create_pon}->{mem},
				hpc_driver	=> $args{hpc_driver},
				extra_args	=> [$hpc_group]
				);

			$pon_run_id = submit_job(
				jobname		=> 'run_panel_msi__create_pon',
				shell_command	=> $run_script,
				hpc_driver	=> $args{hpc_driver},
				dry_run		=> $args{dry_run},
				log_file	=> $log
				);

			push @all_jobs, $pon_run_id;
			} else {
			print $log ">> Skipping CreatePoN step because this has already been completed!\n";
			}
		}

	# continue processing each sample in $smp_data
	foreach my $patient (sort keys %{$smp_data}) {

		# find sample ids
		my @normal_ids = keys %{$smp_data->{$patient}->{'normal'}};
		my @tumour_ids = keys %{$smp_data->{$patient}->{'tumour'}};

		# create some directories
		my $patient_directory = join('/', $output_directory, $patient);
		my $link_directory = join('/', $patient_directory, 'bam_links');

		# indicate normal to use
		my $normal_rc = $pon;
		my $normal_job = $pon_run_id;
		if (scalar(@normal_ids) > 0) {
			$normal_rc = join('/', $pon_directory, $normal_ids[0] . '_readcounts.tsv');
			$normal_job = join(':', @{$normal_jobs{$patient}});
			}

		# for each tumour sample
		foreach my $sample (@tumour_ids) {

			print $log "\n  TUMOUR: $sample\n";

			my $sample_directory = join('/', $patient_directory, $sample);
			unless(-e $sample_directory) { make_path($sample_directory); }

			# find input bam
			my $bam = basename($smp_data->{$patient}->{tumour}->{$sample});
			$link = join('/', $link_directory, basename($bam));
			symlink($bam, $link);

			# indicate output stem
			my $tumour_rc = join('/', $sample_directory, $sample . '_readcounts.tsv');

			# create readCounter command
			my $make_rc_command = get_msi_command(
				step	=> 'get_readcounts',
				intervals	=> $annotated_targets,
				bam		=> $smp_data->{$patient}->{tumour}->{$sample},
				output_file	=> $tumour_rc
				);

			# check if this should be run
			if ('Y' eq missing_file($tumour_rc)) {

				# record command (in log directory) and then run job
				print $log "  >> Submitting job for readCounter...\n";

				$run_script = write_script(
					log_dir	=> $log_directory,
					name	=> 'run_readCounter_' . $sample,
					cmd	=> $make_rc_command,
					modules	=> [$r_version],
					dependencies	=> $step1_run_id,
					max_time	=> $parameters->{readcounter}->{time},
					mem		=> $parameters->{readcounter}->{mem},
					hpc_driver	=> $args{hpc_driver},
					extra_args	=> [$hpc_group]
					);

				$run_id = submit_job(
					jobname		=> 'run_readCounter_' . $sample,
					shell_command	=> $run_script,
					hpc_driver	=> $args{hpc_driver},
					dry_run		=> $args{dry_run},
					log_file	=> $log
					);

				push @{$patient_jobs{$patient}}, $run_id;
				push @all_jobs, $run_id;
				} else {
				print $log "  >> Skipping readCounter because this has already been completed!\n";
				}

			# indicate final output file
			my $final_file = join('/',
				$sample_directory, 
				$sample . '_pertarget_msi_metrics.tsv'
				);

			# run on provided readcount files
			my $calc_msi_command = get_msi_command(
				step	=> 'final',
				intervals	=> $annotated_targets,
				tumour_rc	=> $tumour_rc,
				normal_rc	=> $normal_rc,
				output_file	=> $final_file
				);

			# check if this should be run
			if ('Y' eq missing_file($final_file)) {

				# record command (in log directory) and then run job
				print $log "  >> Submitting job for callMSI...\n";

				$run_script = write_script(
					log_dir	=> $log_directory,
					name	=> 'run_call_msi_' . $sample,
					cmd	=> $calc_msi_command,
					modules	=> [$r_version],
					dependencies	=> join(':', $run_id, $normal_job),
					max_time	=> $parameters->{calc_msi}->{time},
					mem		=> $parameters->{calc_msi}->{mem},
					hpc_driver	=> $args{hpc_driver},
					extra_args	=> [$hpc_group]
					);

				$run_id = submit_job(
					jobname		=> 'run_call_msi_' . $sample,
					shell_command	=> $run_script,
					hpc_driver	=> $args{hpc_driver},
					dry_run		=> $args{dry_run},
					log_file	=> $log
					);

				push @{$patient_jobs{$patient}}, $run_id;
				push @all_jobs, $run_id;
				} else {
				print $log "  >> Skipping callMSI step because this has already been completed!\n";
				}

			push @{$patient_jobs{$patient}}, @{$normal_jobs{$patient}};
			push @{$final_outputs{$patient}}, $final_file;
			}

		# should intermediate files be removed
		# run per patient
		if ($args{del_intermediates}) {

			unless (scalar(@{$patient_jobs{$patient}}) == 0) {

				print $log ">> Submitting job to clean up temporary/intermediate files...\n";

				# make sure final output exists before removing intermediate files!
				my $cleanup_cmd = join("\n",
					"if [ -s " . join(" ] && [ -s ", @{$final_outputs{$patient}}) . " ]; then",
					"  $cleanup{$patient}",
					"else",
					'  echo "One or more FINAL OUTPUT FILES is missing; not removing intermediates"',
					"fi"
					);

				$run_script = write_script(
					log_dir	=> $log_directory,
					name	=> 'run_cleanup_' . $patient,
					cmd	=> $cleanup_cmd,
					dependencies	=> join(':', @{$patient_jobs{$patient}}),
					mem		=> '256M',
					hpc_driver	=> $args{hpc_driver},
					kill_on_error	=> 0,
					extra_args	=> [$hpc_group]
					);

				$run_id = submit_job(
					jobname		=> 'run_cleanup_' . $patient,
					shell_command	=> $run_script,
					hpc_driver	=> $args{hpc_driver},
					dry_run		=> $args{dry_run},
					log_file	=> $log
					);
				}
			}

		print $log "\nFINAL OUTPUT:\n" . join("\n  ", @{$final_outputs{$patient}}) . "\n";
		print $log "---\n";
		}

	# collate results
	if ($should_run_final) {

		my $collect_output = join(' ',
			"Rscript $cwd/collect_panelMSI_output.R",
			'-d', $output_directory,
			'-p', $tool_data->{project_name}
			);

		$run_script = write_script(
			log_dir	=> $log_directory,
			name	=> 'combine_panelMSI_output',
			cmd	=> $collect_output,
			modules	=> [$r_version],
			dependencies	=> join(':', @all_jobs),
			mem		=> '4G',
			max_time	=> '12:00:00',
			hpc_driver	=> $args{hpc_driver},
			extra_args	=> [$hpc_group]
			);

		$run_id = submit_job(
			jobname		=> 'combine_panelMSI_output',
			shell_command	=> $run_script,
			hpc_driver	=> $args{hpc_driver},
			dry_run		=> $args{dry_run},
			log_file	=> $log
			);

		push @all_jobs, $run_id;
		}

	# if this is not a dry run OR there are jobs to assess (run or resumed with jobs submitted) then
	# collect job metrics (exit status, mem, run time)
	unless ( ($args{dry_run}) || (scalar(@all_jobs) == 0) ) {

		# collect job stats
		my $collect_metrics = collect_job_stats(
			job_ids		=> join(',', @all_jobs),
			outfile		=> $outfile,
			hpc_driver	=> $args{hpc_driver}
			);

		$run_script = write_script(
			log_dir	=> $log_directory,
			name	=> 'output_job_metrics_' . $run_count,
			cmd	=> $collect_metrics,
			dependencies	=> join(':', @all_jobs),
			mem		=> '256M',
			hpc_driver	=> $args{hpc_driver},
			kill_on_error	=> 0,
			extra_args	=> [$hpc_group]
			);

		$run_id = submit_job(
			jobname		=> 'output_job_metrics',
			shell_command	=> $run_script,
			hpc_driver	=> $args{hpc_driver},
			dry_run		=> $args{dry_run},
			log_file	=> $log
			);

		push @all_jobs, $run_id;

		# do some logging
		print "Number of jobs submitted: " . scalar(@all_jobs) . "\n";

		my $n_queued = `squeue -r | wc -l`;
		print "Total number of jobs in queue: " . $n_queued . "\n";

		# wait until it finishes
		unless ($args{no_wait}) {
			check_final_status(job_id => $run_id);
			}
		}

	# finish up
	print $log "\nProgramming terminated successfully.\n\n";
	close $log;
	}

### GETOPTS AND DEFAULT VALUES #####################################################################
# declare variables
my ($tool_config, $data_config, $output_directory);
my $hpc_driver = 'slurm';
my ($remove_junk, $dry_run, $help, $no_wait);

# get command line arguments
GetOptions(
	'h|help'	=> \$help,
	'd|data=s'	=> \$data_config,
	't|tool=s'	=> \$tool_config,
	'o|out_dir=s'	=> \$output_directory,
	'c|cluster=s'	=> \$hpc_driver,
	'remove'	=> \$remove_junk,
	'dry-run'	=> \$dry_run,
	'no-wait'	=> \$no_wait
	);

if ($help) {
	my $help_msg = join("\n",
		"Options:",
		"\t--help|-h\tPrint this help message",
		"\t--data|-d\t<string> data config (yaml format)",
		"\t--tool|-t\t<string> tool config (yaml format)",
		"\t--out_dir|-o\t<string> path to output directory",
		"\t--cluster|-c\t<string> cluster scheduler (default: slurm)",
#		"\t--remove\t<boolean> should intermediates be removed? (default: false)",
		"\t--dry-run\t<boolean> should jobs be submitted? (default: false)",
		"\t--no-wait\t<boolean> should we exit after job submission (true) or wait until all jobs have completed (false)? (default: false)"
		);

	print "$help_msg\n";
	exit;
	}

# do some quick error checks to confirm valid arguments	
if (!defined($tool_config)) { die("No tool config file defined; please provide -t | --tool (ie, tool_config.yaml)"); }
if (!defined($data_config)) { die("No data config file defined; please provide -d | --data (ie, sample_config.yaml)"); }
if (!defined($output_directory)) { die("No output directory defined; please provide -o | --out_dir"); }

main(
	tool_config		=> $tool_config,
	data_config		=> $data_config,
	output_directory	=> $output_directory,
	hpc_driver		=> $hpc_driver,
#	del_intermediates	=> $remove_junk,
	dry_run			=> $dry_run,
	no_wait			=> $no_wait
	);
